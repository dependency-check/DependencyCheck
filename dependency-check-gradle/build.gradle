/*
 * This file is part of dependency-check-gradle.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * Copyright (c) 2015 Wei Ma. All Rights Reserved.
 */

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.0"
    }
}

plugins {
    id 'nu.studer.plugindev' version '1.0.3'
}

apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: "com.gradle.plugin-publish"

repositories {
    mavenCentral()
}

dependencies {
    compile(
            localGroovy(),
            gradleApi(),
            'org.owasp:dependency-check-core:1.2.11',
            'org.owasp:dependency-check-utils:1.2.10'
    )
}

group = 'com.thoughtworks.tools'
version = '0.0.4'

//-------------------------------
// Local debug use only
//
//uploadArchives {
//    repositories {
//        mavenDeployer {
//            repository(url: uri('../../../repo'))
//        }
//    }
//}
//-------------------------------


// upload to maven central
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'Dependency Check Gradle Plugin'
                packaging 'jar'
                description 'Dependency-Check is a utility that attempts to detect publicly disclosed vulnerabilities contained within project dependencies.'
                url 'https://github.com/wmaintw/DependencyCheck/tree/master/dependency-check-gradle'

                scm {
                    connection 'git@github.com:wmaintw/DependencyCheck.git'
                    developerConnection 'git@github.com:wmaintw/DependencyCheck.git'
                    url 'https://github.com/wmaintw/DependencyCheck'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'wmaintw'
                        name 'Wei Ma'
                        email 'wma@thoughtworks.com'
                    }
                }
            }
        }
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

// publish to Bintray
plugindev {
    pluginId = 'dependency.check'
    pluginName = 'dependency-check'
    pluginImplementationClass 'com.tools.security.plugin.DependencyCheckGradlePlugin'
    pluginDescription 'This is dependency check gradle plugin.'
    pluginLicenses 'Apache-2.0'
    pluginTags 'dependency check', 'security'
    authorId 'wmaintw'
    authorName 'Wei Ma'
    authorEmail 'wma@thoughtworks.com'
    projectUrl 'https://github.com/wmaintw/DependencyCheck'
    projectIssuesUrl 'https://github.com/wmaintw/DependencyCheck/issues'
    projectVcsUrl 'git@github.com:wmaintw/DependencyCheck.git'
    projectInceptionYear '2015'
    done()
}

bintray {
    user = bintrayUser
    key = bintrayUserKey
    pkg.repo = bintrayRepo
}

// publish to gradle plugin portal
pluginBundle {
    website = 'https://github.com/wmaintw/DependencyCheck'
    vcsUrl = 'git@github.com:wmaintw/DependencyCheck.git'
    description = 'This is dependency check gradle plugin.'
    tags = ['dependency check', 'security']

    plugins {
        dependencyCheckPlugin {
            id = 'dependency.check'
            displayName = 'dependency-check'
        }
    }
}